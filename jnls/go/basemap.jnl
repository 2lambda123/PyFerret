\cancel mode verify
! basemap.jnl - 10/94 *sh*

! Description: Plot a base map of the earth (solid, outline, or detailed) 

!              arg 1     arg 2   arg 3   arg 4     arg 5   arg 6  arg 7  arg 8
! GO basemap [x=lo:hi] [y=lo:hi] [res] [palette] [detail] [land] [natl] [state]

! arg 1 - longitude limits - specify as for any FERRET longitude range.
!	  To produce a 360 degree view centered on Greenwhich use X=180:540.
!     					default = 20e to 380 (range of etoponn)
! arg 2 - latitude limits - specify as for any FERRET latitude range. 
!	  		     		default 90s to 90n (range of etoponn)
! arg 3 - resolution - may be 120,60,40,20,10, or 5     default=60
!	  To use resolution "nnn" the file etoponnn must exist.
! arg 4 - palette - may be any palette on your system.  Normally solid colors
!		    like black, gray,red, etc. (see Fpalette '*') default=gray
! arg 5 - detailed - may be "detailed",  "hollow", "all", or "solid"  (default)
! arg 6 - Color (and thickness) of coastal outline drawn. default=none
!         The color may be any of the acceptable colors from GO land. 
!	  (Use "N" to omit coastlines but include nation or state boundaries)
! arg 7 - Color (and thickness) of national boundaries drawn.   default=none
!         The color may be any of the acceptable colors from GO land. 
! arg 8 - Color (and thickness) of US state boundaries drawn.   default=none
!         The color may be any of the acceptable colors from GO land. 

! examples:
!	GO basemap
!	GO basemap x=140w:360 y=-15:40
!	GO basemap x=140w:360 y=-15:40 20
!	GO basemap x=140w:360 y=-15:40 20        red
!	GO basemap x=140w:360 y=-15:40 20 dark_terrestrial detailed
!	GO basemap x=140w:360 y=-15:40 20      land_sea      all
!	GO basemap   " "        " "   " "        red        solid  thick
!	GO basemap   " "        " "   " "        " "        hollow  red
!	GO basemap  x=140w:10e y=30s:70n 60    land_sea      all  black red red

! check the overlay/basemap option
query/ignore $5%not_required|detailed|hollow|solid|all> <5th argument of GO basemap may be "detailed", "hollow" or "solid" or "all" (ocean, too)%
query/ignore $6%1|black|red|green|blue|thick|0|1|2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|N|<line_color may be 0(back), 1(fore), thick, 2-18 or a color%

! initialize
set data/save

! do the shaded plot
set data etopo$3"60"
shade/nolab/nokey/$1"nolab"/$2"nolab"/lev=$5"(0,10000,10000)|detailed>(0,200,50)(200,1000,100)(1000,10000,1000)|solid>(0,10000,10000)|hollow>(20000,20000,1)|all>(-200,200,50)(-1000,1000,200)(-8000,8000,1000)"/pal=$4"gray" rose
cancel data etopo$3"60"
set data/restore

! optionally overlay the conintental boundaries
$6"query/ignore this|*>GO land" $6"dummy" overlay $7" " $8" "

set mode/last verify
