#! /bin/csh

if ( $#argv != 2 ) then
   echo "Usage:  $0  <svn_repository>  <target_dir> "
   echo ""
   echo "    The required files will be extracted from <svn_repository> "
   echo "    (e.g., 'file:///home/users/tmap/svn/repos/ferret/trunk') "
   echo "    to a temporary directory which this script will create. "
   echo "    The gzipped tar file fer_source.tar.gz will be written in "
   echo "    <target_dir>, which must already exist. "
   echo ""
   exit 1
endif

set info = `svn info "$1"`
if ( "${info}" == "" ) then
#  svn has printed an appropriate error message
#  (but still returned a zero status)
   echo ""
   exit 1
endif
set repository = "$1"

if ( ! -d "$2" ) then
   echo "$2 does not exist or is not a directory "
   echo ""
   exit 1
endif
# Make sure target_dir is a full pathname
set target_dir = `cd "$2" ; pwd`

# Decide what to call the directory checked-out from svn
set fer_name = ${repository:t}
if ( ${fer_name} == "trunk" ) then
   set datestamp = `date +%F`
   set fer_name = "ferret_${datestamp}"
else if ( ${fer_name} == "pyferret" ) then
   set datestamp = `date +%F`
   set fer_name = "pyferret_${datestamp}"
endif

# Make a clean temporary directory for the tar file contents
set temp_dir = "/var/tmp/fer_src_$$"
rm -fr ${temp_dir}
mkdir ${temp_dir}
cd ${temp_dir}

# Checkout the required files
echo "Extracting FERRET source code "
echo "from ${repository} "
echo "to ${temp_dir}/${fer_name} "
svn checkout -q ${repository} ${fer_name}

# Remove the html_docs subdirectory from the checked-out directory
rm -rf ${fer_name}/html_docs

# Remove threddsBrowser/toolsUI/toolsUI-4.1.jar for the check-out directory
# Have the user download this file from ucar
echo ""
echo "NOTE: To minimize size, the toolsUI-4.1.jar file is not included."
echo "The user needs to download the jar file from:"
echo "  ftp://ftp.unidata.ucar.edu/pub/netcdf-java/v4.1/toolsUI-4.1.jar"
echo "(or we can just grab it from <ferret_dir>/threddsBrowser/toolsUI/)"
echo 'and put it under the $FER_LIBS ($FER_DIR/lib) subdirectory'
rm ${fer_name}/threddsBrowser/toolsUI/toolsUI-4.1.jar

# Remove the bench/metafile_masters_* directories as this time.  GKS metafiles
# no longer produced, and currently not creating the PostScript files in RUN_TESTS.sh
echo ""
echo 'NOTE: The bench/metafile_masters_* directories are not included.'
rm -fr ${fer_name}/bench/metafile_masters_*

# Create the tar file
set ctar_file = "${target_dir}/fer_source.tar.gz"
echo ""
echo "The tar file will be created from the contents "
echo "(except for the .svn subdirectories) of "
echo "${temp_dir}"
echo "(which can now be examined or tweaked from another shell/window)"
echo ""
echo -n "Create gzipped tar file ${ctar_file} (y/n)? "
set ans = $<
while ( ("${ans}" != "y") && ("${ans}" != "n") )
   echo -n "Answer either y or n: "
   set ans = $<
end
if ( "${ans}" == "y" ) then
   echo ""
   rm -f "${ctar_file}"
   tar cvzf "${ctar_file}" --exclude .svn "${fer_name}"
   echo ""
   ls -l "${ctar_file}"
else
   echo ""
   echo "Tar file NOT created"
endif

# Clean up
echo ""
echo "Cleaning up - removing ${temp_dir}"
cd "${target_dir}"
rm -fr "${temp_dir}"
echo ""

