#! /bin/csh -x
# make_environment_tar
# ver 1.0 4/94 *sh*
# ver 2.0 5/99 *kob* massive upgrade to include CVS
# ver 3.0 11/10 *acm* change from CVS to svn

# this script encourages the tar file producer to make several manual
# sanity checks but it does not enforce behavior

#Make sure we are running on an OSF machine - only kind right now which can
# run svn
set thost = `uname -s`

if ( $#argv == 2 ) then
   set targ_dist_parent = $argv[1]
   set machine_type = $argv[2]
   set source_dir = $targ_dist_parent/$machine_type
else
   echo ""
   echo usage:
   echo make_environment_tar target_area machine_type
   echo machine_type may be solaris, alpha, sgi, linux, x86_64-linux, or win32
   echo e.g. "'make_environment_tar /home/users/ansley/ferret_install_utilities x86_64-linux'" will extract  
   echo files for the Ferret environment in the directory
   echo "'/home/users/ansley/ferret_install_utilities/x86_64-linux'"
   echo and ask for a location to place the gzipped tar file
   echo ""
 exit 0
endif

cd $source_dir

#use svn now to checkout desired files
echo "Extracting FERRET environment files......."
svn checkout file:///home/users/tmap/svn/repos/ferret/trunk/bin >& .install_log
svn checkout file:///home/users/tmap/svn/repos/ferret/trunk/jnls >>& .install_log
svn checkout file:///home/users/tmap/svn/repos/ferret/trunk/external_functions >>& .install_log
svn checkout file:///home/users/tmap/svn/repos/ferret/trunk/palettes >>& .install_log
svn checkout file:///home/users/tmap/svn/repos/ferret/trunk/bin >>& .install_log

#Now move these files into the proper position
echo "Doing a bit of rearranging now......."
mv jnls/* .
rm -rf jnls
mv palettes ppl
mkdir ext_func
mv external_functions ext_func/src


#Copy fonts
echo "Copying fonts......."
cd ppl >& /dev/null
mkdir fonts
cd fonts >& /dev/null
if ($machine_type == linux || $machine_type == win32) then
    cp -p /home/users/tmap/ferret/linux/ppl/fonts/f* .
else
    cp -p /home/users/tmap/ferret/x86_64-linux/ppl/fonts/f* .
endif

#cd ../.. >& /dev/null
pwd
cd ../.. 
pwd

# Now set up the proper soft links
echo "Setting Up links......."
# cd bin >& /dev/null
cd bin 
ln -s Fdescr Fdesc
ln -s Fgrids Fgrid
ln -s Fprint_template Fprint

# Remove refs to old doc/ directory
rm -f Fapropos*
rm -f Fhelp*
rm -f Findex*
rm -f Ftoc*
cd .. >& /dev/null

pwd

mkdir lib
cd lib 
cp $FER_DIR/lib/threddsBrowser.jar .
cp $FER_DIR/lib/toolsUI-4.1.jar .
ln -s toolsUI-4.1.jar toolsUI.jar
cd ../

# get tar file location
echo "Enter directory path to create the gzipped tar file in"
set ans = $<
set ctar_file = $ans/fer_environment.tar
echo "Ready to create gzipped tar file $ctar_file"
ready:
echo "OK to continue ?"
set ans = $<
switch($ans)
case y:
   breaksw
case n:
   exit 0
default:
   echo "must answer y or n"
   goto ready
endsw

# make the gzipped tar file
cd $source_dir
rm -f .tar_create_log
tar vcf $ctar_file  --exclude .svn *  >& ./tar_create_log
gzip $ctar_file
ls -l $ctar_file*
echo "If there are any problems w/ the tar file, see "$source_dir/".tar_create_log for"
echo "more information"




