#! /bin/csh
## This c-shell script is used to set environment variables need by
## pyferret, and then execute ferret.py in python.  The intent of this
## script is to provide a traditional Ferret interface through pyferret.

## Assign the python2.x executable to use
# set pyname = python2.4
set pyname = python2.6

## Assign the Python directory containing the pyferret package and ferret.py
## One of:
## (a) the default user-specific site
set pysite = ${HOME}/.local/lib/${pyname}/site-packages
## or:
## (b) the default system-wide site, typically one of:
##         /usr/lib/python2.x/site-packages
##         /usr/local/lib/python2.x/site-packages
##         /usr/lib64/python2.x/site-packages
##     Run: 
##        python2.x -c "import sys; print sys.path"
##     to show locations searched.
# set pysite = /usr/lib/${pyname}/site-packages
## or:
## (c) a custom directory (for example, if installed under $FER_DIR)
# if ( $?FER_DIR ) then
#     set pysite = ${FER_DIR}/lib/${pyname}/site-packages
# endif


## Make sure the FER_* environment variables are assigned
if ( ! $?FER_DIR ) then
##   Either source the ferret_paths script to assign the environment variables
#   source /my/path/to/ferret_paths >&! /dev/null
##   or just throw an error if they should have already been defined
    echo "**ERROR: Ferret environment variables are not defined"
    exit 1
##
endif

## FER_LIBS may not be defined by older ferret_paths scripts.  
## This environment variable names the directory containing 
## threddsBrowser.jar and toolsUI.jar.  It is only needed to
## run the ThreddsBrowser GUI ('USE /BROWSE' Ferret command).  
if ( ! $?FER_LIBS ) then
    setenv FER_LIBS ${FER_DIR}/lib
endif
## The environment variable JAVA_HOME (for Java version 1.6.x) 
## may also need to be defined in order to run the ThreddsBrowser GUI.
## The directory defined by this environment variable contains 
## the executable java in the bin subdirectory (ie, bin/java).
if ( ! $?JAVA_HOME ) then
     setenv JAVA_HOME /usr/java/latest
#     setenv JAVA_HOME /usr/lib/jvm/java-1.6.0-sun
endif

## Add pysite to the Python search path given by PYTHONPATH
if ( ! $?PYTHONPATH ) then
    setenv PYTHONPATH ${pysite}
else
    if ( ${PYTHONPATH} !~ *${pysite}* ) then
        setenv PYTHONPATH ${pysite}:${PYTHONPATH}
    endif
endif

# Finally, start pyferret via the ferret.py script
${pyname} -i ${pysite}/ferret.py $*

